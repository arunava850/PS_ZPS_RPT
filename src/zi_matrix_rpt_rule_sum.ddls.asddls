@AbapCatalog.sqlViewName: 'ZIMATRPTRULESUM'
@AbapCatalog.compiler.compareFilter: true
@AbapCatalog.preserveKey: true
@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'Cons Data with Reporting Rule'
define view ZI_MATRIX_RPT_RULE_SUM 
  with parameters
    P_FISCPER                    : fis_jahrper_conv,
    P_ConsolidationUnitHierarchy : fincs_hryid,
    P_ConsolidationPrftCtrHier   : fincs_hryid,
    P_ConsolidationSegmentHier   : fincs_hryid,
    P_KeyDate                    : vdm_v_key_date,
    P_ConsolidationRptgItemHier  : fincs_rihry,
    P_ConsolidationRptgRuleID    : fincs_rptid,
    P_ChartOfAccount             : fc_itclg 
  as select from   I_MatrixCnsldtnFoundation_2(
                   P_ConsolidationUnitHierarchy   : $parameters.P_ConsolidationUnitHierarchy,
                   P_ConsolidationPrftCtrHier     : $parameters.P_ConsolidationPrftCtrHier,
                   P_ConsolidationSegmentHier     : $parameters.P_ConsolidationSegmentHier,
                   P_ConsolidationGroup           : '',
                   P_KeyDate                      : $parameters.P_KeyDate,
                   P_FromFiscalYearPeriod         : '1900001',
                   P_ToFiscalYearPeriod           : '9999012' ) as  ZTRX
 join ZI_GR_FISCPER_CDS( P_FISCPER : $parameters.P_FISCPER  ) as b  on b.CUR_PER = :P_FISCPER 
 join ZI_GR_RPTRULE_HIER(p_coa : $parameters.P_ChartOfAccount) as c on c.itclg   = ZTRX.ConsolidationChartOfAccounts and
                                                                       c.rihry   = :P_ConsolidationRptgItemHier      and
                                                                       c.rptid   = :P_ConsolidationRptgRuleID        and
                                                                       c.item    = ZTRX.FinancialStatementItem  
{
  ConsolidationLedger,
  ConsolidationDimension,
  FiscalYear,
  ConsolidationDocumentNumber, 
  ConsolidationPostingItem,
  GLRecordType,
  ConsolidationVersion,
  ConsolidationVersionElement,
  TransactionCurrency,
  LocalCurrency,
  GroupCurrency,
  BaseUnit,
  FiscalPeriod,
  FiscalYearPeriod,
  FiscalYearQuarter,
  FiscalQuarter,
  FiscalYearVariant,
  PeriodMode,
  ConsolidationDocumentType,
  DebitCreditCode,
  Company,
  ConsolidationUnit,
  ConsolidationUnitForElim,
  ConsolidationChartOfAccounts,
  FinancialStatementItem,
  PartnerConsolidationUnit,
  ConsolidationGroup,
  CompanyCode,
  SubItemCategory,
  SubItem,
  PostingLevel,
  ConsolidationApportionment,
  CurrencyConversionsDiffType,
  ConsolidationAcquisitionYear,
  ConsolidationAcquisitionPeriod,
  InvesteeConsolidationUnit,
  InvestorConsolidationUnit,
  CnsldtnQuantityInBaseUnit,
  DocumentItemText,
  ConsolidationPostgItemAutoFlag,
  BusinessTransactionType,
  PostingDate,
  CurrencyTranslationDate,
  RefConsolidationDocumentNumber,
  ReferenceFiscalYear,
  RefConsolidationPostingItem,
  RefConsolidationDocumentType,
  RefBusinessTransactionType,
  CreationDate,
  CreationTime,
  UserID,
  ReverseDocument,
  ReversedDocument,
  InvestmentActivityType,
  InvestmentActivity,
  ConsolidationDocReversalYear,
  ReferenceDocumentType,
  ReferenceDocumentContext,
  LogicalSystem,
  ChartOfAccounts,
  GLAccount,
  AssignmentReference,
  CostCenter,
  ProfitCenter,
  ConsolidationPrftCtrForElim,
  FunctionalArea,
  BusinessArea,
  ControllingArea,
  Segment,
  ConsolidationSegmentForElim,
  PartnerCostCenter,
  PartnerProfitCenter,
  PartnerFunctionalArea,
  PartnerBusinessArea,
  PartnerCompany,
  PartnerSegment,
  OrderID,
  Customer,
  Supplier,
  Material,
  Plant,
  FinancialTransactionType,
  WBSElementInternalID,
  WBSElementExternalID,
  Project,
  c.ritem as ConsolidationReportingItem,
  BillingDocumentType,
  SalesOrganization,
  DistributionChannel,
  OrganizationDivision,
  MaterialGroup,
  SoldProduct,
  SoldProductGroup,
  CustomerGroup,
  CustomerSupplierCountry,
  CustomerSupplierIndustry,
  SalesDistrict,
  BillToParty,
  ShipToParty,
  // Additionnal Flieds: AdHoc Item
  CnsldtnAdhocItem,
  CnsldtnAdhocSet,
  CnsldtnAdhocSetItem,
  MatchingReasonCode,
  OriginType,
  OriginReference,  
  CnsldtnGroupJrnlEntryBundle,
  CustomerSupplierCorporateGroup,
  @DefaultAggregation: #SUM
 sum( case  
   when ZTRX.FiscalYearPeriod = b.CUR_PER  then zsign * ZTRX.AmountInGroupCurrency 
   else 0
  end )  as CurrPeriodAmt,

  @DefaultAggregation: #SUM
  sum( case  
   when ZTRX.FiscalYearPeriod = b.PREV_YR_SAME_PER  then zsign * ZTRX.AmountInGroupCurrency 
   else 0
  end )   as PrevPeriodAmt,
 
  @DefaultAggregation: #SUM
  sum( case  
   when ZTRX.FiscalYearPeriod between b.CUR_YR_JAN_FROM and b.CUR_PER  then zsign * ZTRX.AmountInGroupCurrency 
   else 0
  end )   as CurrYTDAmt,

  @DefaultAggregation: #SUM
  sum( case  
   when ZTRX.FiscalYearPeriod between b.PREV_YR_JAN_FROM and b.PREV_YR_SAME_PER  then zsign * ZTRX.AmountInGroupCurrency 
   else 0
  end )   as PrevYTDAmt,

  @DefaultAggregation: #SUM
  sum( case  
   when ZTRX.FiscalYearPeriod between b.PREV_YR2_JAN_FROM and b.PREV_YR2_DEC_TO  then zsign * ZTRX.AmountInGroupCurrency 
   else 0 
  end ) as PrevYR2YTDAmt,  
       
  _Ledger,
  _Dimension,
  _Version,  
  _VersionElement,
  _Company,
  _CnsldtnUnit,
  _CnsldtnUnitForElimination,
  _CnsldtnGroup,
  _ChartOfAccounts,
  _FinStmntItm,
  _CnsldtnFSItem_2,
  _GLAccountInChartOfAccounts,
  _GLChartOfAccounts,
  _PartnerUnit,
  _CompanyCode,
  _SubItemCategory,
  _SubItem,
  _DebitCreditCode,
  _DocumentType,
  _PostingLevel,
  _InvesteeUnit,
  _InvestorUnit,
  _Apportionment,
  _CrcyCnvrsnDiffType,
  _TransactionCurrency,
  _LocalCurrency,
  _GroupCurrency,
  _BaseUnit,
  _InternalOrder,
  _Customer,
  _Supplier,
  _Material,
  _MaterialGroup,
  _Plant,
  _FinancialTransactionType,
  _WBSElement,
  _WBSElementInternalID,
  _Project,
  _CostCenter,
  _ProfitCenter,
  _ProfitCenterForElim,
  _FunctionalArea,
  _BusinessArea,
  _ControllingArea,
  _Segment,
  _SegmentForElim,
  _PartnerCostCenter,
  _PartnerProfitCenter,
  _PartnerFunctionalArea,
  _PartnerBusinessArea,
  _PartnerCompany,
  _PartnerSegment,
  _BillingDocumentType,
  _SalesOrganization,
  _DistributionChannel,
  _Division,
  _SoldProduct,
  _SoldProductGroup,
  _CustomerGroup,
  _Country,
  _Industry,
  _SalesDistrict,
  _BillToParty,
  _ShipToParty,
  _AdhocItem,
  _AdhocSet,
  _AdhocSetItem
}
group by
  ConsolidationLedger,
  ConsolidationDimension,
  FiscalYear,
  ConsolidationDocumentNumber, 
  ConsolidationPostingItem,
  GLRecordType,
  ConsolidationVersion,
  ConsolidationVersionElement,
  TransactionCurrency,
  LocalCurrency,
  GroupCurrency,
  BaseUnit,
  FiscalPeriod,
  FiscalYearPeriod,
  FiscalYearQuarter,
  FiscalQuarter,
  FiscalYearVariant,
  PeriodMode,
  ConsolidationDocumentType,
  DebitCreditCode,
  Company,
  ConsolidationUnit,
  ConsolidationUnitForElim,
  ConsolidationChartOfAccounts,
  FinancialStatementItem,
  PartnerConsolidationUnit,
  ConsolidationGroup,
  CompanyCode,
  SubItemCategory,
  SubItem,
  PostingLevel,
  ConsolidationApportionment,
  CurrencyConversionsDiffType,
  ConsolidationAcquisitionYear,
  ConsolidationAcquisitionPeriod,
  InvesteeConsolidationUnit,
  InvestorConsolidationUnit,
  CnsldtnQuantityInBaseUnit,
  DocumentItemText,
  ConsolidationPostgItemAutoFlag,
  BusinessTransactionType,
  PostingDate,
  CurrencyTranslationDate,
  RefConsolidationDocumentNumber,
  ReferenceFiscalYear,
  RefConsolidationPostingItem,
  RefConsolidationDocumentType,
  RefBusinessTransactionType,
  CreationDate,
  CreationTime,
  UserID,
  ReverseDocument,
  ReversedDocument,
  InvestmentActivityType,
  InvestmentActivity,
  ConsolidationDocReversalYear,
  ReferenceDocumentType,
  ReferenceDocumentContext,
  LogicalSystem,
  ChartOfAccounts,
  GLAccount,
  AssignmentReference,
  CostCenter,
  ProfitCenter,
  ConsolidationPrftCtrForElim,
  FunctionalArea,
  BusinessArea,
  ControllingArea,
  Segment,
  ConsolidationSegmentForElim,
  PartnerCostCenter,
  PartnerProfitCenter,
  PartnerFunctionalArea,
  PartnerBusinessArea,
  PartnerCompany,
  PartnerSegment,
  OrderID,
  Customer,
  Supplier,
  Material,
  Plant,
  FinancialTransactionType,
  WBSElementInternalID,
  WBSElementExternalID,
  Project,
  c.ritem,
  BillingDocumentType,
  SalesOrganization,
  DistributionChannel,
  OrganizationDivision,
  MaterialGroup,
  SoldProduct,
  SoldProductGroup,
  CustomerGroup,
  CustomerSupplierCountry,
  CustomerSupplierIndustry,
  SalesDistrict,
  BillToParty,
  ShipToParty,
  CnsldtnAdhocItem,
  CnsldtnAdhocSet,
  CnsldtnAdhocSetItem,
  MatchingReasonCode,
  OriginType,
  OriginReference,  
  CnsldtnGroupJrnlEntryBundle,
  CustomerSupplierCorporateGroup
